import org.gradle.plugins.ide.eclipse.model.Classpath

apply plugin: 'maven'
apply plugin: 'signing'

def quickfixVersion = "1.7.0-SNAPSHOT"

defaultTasks 'clean', 'build', 'install'

ext {
    group = fixGroup
    version = fixVersion

    if (!project.hasProperty('ossrhUsername'))
        ossrhUsername = ''

    if (!project.hasProperty('ossrhPassword'))
        ossrhPassword = ''
}

allprojects {
    apply plugin: 'eclipse'

    repositories {
        mavenCentral()
        mavenLocal()
        maven {
            url "http://repo.marketcetera.org/maven"
        }
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'idea'

    // Make sure that we use the correct JDK in Eclipse
    // There needs to be a JDK8 JVM installed in Eclipse before
    // importing the generated projects.
    plugins.withType(JavaBasePlugin) {
        eclipse.classpath.file.whenMerged { Classpath path ->
            path.entries.findAll { it.path.contains('JRE_CONTAINER') }.each {
                it.path += "/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.8"
            }
        }
    }

    group = fixGroup
    version = fixVersion

    [compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

    dependencies {
        compile "uk.co.real-logic:artio-codecs:${fixVersion}"

        testCompile 'junit:junit:4.12'
        testCompile 'org.mockito:mockito-core:2.19.0'
        testCompile 'org.hamcrest:hamcrest-library:1.3'
        testCompile "org.quickfixj:quickfixj-messages-fix42:${quickfixVersion}"
        testCompile "org.quickfixj:quickfixj-messages-fix44:${quickfixVersion}"
        testCompile "org.quickfixj:quickfixj-messages-fix50:${quickfixVersion}"
        testCompile "org.quickfixj:quickfixj-core:${quickfixVersion}"
        testCompile 'ch.qos.logback:logback-classic:1.1.3'
        testCompile 'org.apache.mina:mina-core:2.0.9'
        testCompile "uk.co.real-logic:artio-core:${fixVersion}"

        testCompile group: 'uk.co.real-logic', name: 'artio-codecs', version: "${fixVersion}", classifier: 'tests'
        testCompile group: 'uk.co.real-logic', name: 'artio-core', version: "${fixVersion}", classifier: 'tests'
        testCompile group: 'uk.co.real-logic', name: 'artio-system-tests', version: "${fixVersion}", classifier: 'tests'
    }

    test {
        testLogging {
            events 'skipped', 'failed'
            showStandardStreams = true
            exceptionFormat = 'full'
            afterSuite { desc, result ->
                if (!desc.parent) {
                    println "Results: ${result.resultType} (${result.testCount} tests," +
                    " ${result.successfulTestCount} successes," +
                    " ${result.failedTestCount} failures," +
                    " ${result.skippedTestCount} skipped)"
                }
            }
        }

        beforeTest { desc ->
            if (System.properties["printTestNames"] != null)
            {
                print "Executing test ${desc.name} [${desc.className}]"
            }
        }

        afterTest { desc, result ->
            if (System.properties["printTestNames"] != null)
            {
                println " with result: ${result.resultType}"
            }
        }
    }

    configurations {
        tests
    }

    configurations.all {
        exclude module: 'artio-session-codecs'
        exclude module: 'artio-session-fixt-codecs'
    }

    test {
        systemProperties('java.net.preferIPv4Stack': true)
    }

    idea {
        module {
            downloadJavadoc = true
            downloadSources = true
        }
    }
}

project(':test-framework') {
    apply plugin: 'checkstyle'

    dependencies {
        checkstyle 'com.puppycrawl.tools:checkstyle:8.10.1'
    }

    checkstyle {
        configFile = new File(rootDir, 'config/checkstyle/checkstyle.xml')
        toolVersion = "8.10.1"
    }

    task testJar(type: Jar, dependsOn: testClasses) {
        classifier = 'tests'
        baseName = "test-${project.archivesBaseName}"
        from sourceSets.test.output
    }

    artifacts {
        archives testJar
        tests testJar
    }
}

def getGeneratedDir(project) {
    file("${project.buildDir}/generated-src")
}

configure([project(':fix-42-codecs'), project(':fix-44-codecs'), project(':fix-50-codecs')]) {

    def generatedDir = getGeneratedDir(project)

    sourceSets {
        generated.java.srcDir generatedDir
    }

    compileGeneratedJava.dependsOn 'generateCodecs'
    compileGeneratedJava.classpath += sourceSets.main.runtimeClasspath

    jar {
        from("$buildDir/classes/generated") {
            include '**/*.class'
        }
    }

    jar.dependsOn compileGeneratedJava
}

project(':fix-42-codecs') {
    def generatedDir = getGeneratedDir(project)

    task generateCodecs(type: JavaExec) {
        main = 'uk.co.real_logic.artio.dictionary.CodecGenerationTool'
        classpath = sourceSets.main.runtimeClasspath
        args = [generatedDir, '../../quickfixj/quickfixj-messages/quickfixj-messages-fix42/src/main/resources/FIX42.xml']
        outputs.dir generatedDir
    }
}

project(':fix-44-codecs') {
    def generatedDir = getGeneratedDir(project)

    task generateCodecs(type: JavaExec) {
        main = 'uk.co.real_logic.artio.dictionary.CodecGenerationTool'
        classpath = sourceSets.main.runtimeClasspath
        args = [generatedDir, '../../quickfixj/quickfixj-messages/quickfixj-messages-fix44/src/main/resources/FIX44.xml']
        outputs.dir generatedDir
    }
}

project(':fix-50-codecs') {
    def generatedDir = getGeneratedDir(project)

    task generateCodecs(type: JavaExec) {
        main = 'uk.co.real_logic.artio.dictionary.CodecGenerationTool'
        classpath = sourceSets.main.runtimeClasspath
        args = [generatedDir, '../../quickfixj/quickfixj-messages/quickfixj-messages-fixt11/src/main/resources/FIXT11.xml;' +
                '../../quickfixj/quickfixj-messages/quickfixj-messages-fix50sp2/src/main/resources/FIX50SP2.xml']
        outputs.dir generatedDir
    }
}

project(':test-fix-42') {
    dependencies {
        compile project(path: ':fix-42-codecs')
        testCompile project(path: ':test-framework', configuration: 'tests')
    }

    idea {
        dependencies {
            compile project(':fix-42-codecs').sourceSets.generated.output
        }
    }
}

project(':test-fix-44') {
    dependencies {
        compile project(path: ':fix-44-codecs')
        testCompile project(path: ':test-framework', configuration: 'tests')
    }

    idea {
        dependencies {
            compile project(':fix-44-codecs').sourceSets.generated.output
        }
    }
}

project(':test-fix-50') {
    dependencies {
        compile project(path: ':fix-50-codecs')
        testCompile project(path: ':test-framework', configuration: 'tests')
    }

    idea {
        dependencies {
            compile project(':fix-50-codecs').sourceSets.generated.output
        }
    }
}

wrapper {
    gradleVersion = '4.8.1'
    distributionType = 'ALL'
}
